# Maintainer: Timothy Norman Murphy <tnmurphy@gmail.com>
# Contributor: David Boyce <david.boyce@broadcom.com>
pkgname=extramake
pkgbase=extramake
pkgver=r26.35fc32c
pkgrel=1
pkgdesc="A library of useful functions for GNU Make that are written in C"
url="https://github.com/tnmurphy/extramake"
arch=('x86_64') # if someone wants to try it on arm please contribute any changes!
license=('GPL')
depends=('make')
conflicts=("extramake")
makedepends=('git')
source=("git+https://github.com/tnmurphy/extramake.git#branch=main")

sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname}" && (
        set -o pipefail
        git describe --long --tags 2>/dev/null | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

package() {
  cd "${srcdir}/${pkgname}"

  local _licensedir="${pkgdir}/usr/share/licenses/${pkgname}"
  mkdir -p "${_licensedir}"
  cp -vf COPYING "${_licensedir}"

  local _p="${pkgver}"
  local _installbase="${pkgdir}/usr/lib/${pkgname}"
  local _installdir="${_installbase}/${_p}"
  make XTRA_INSTALLDIR="${_installdir}" install
  ln -s "${_p}" "${_installbase}/latest" 
}

# vim:set ts=2 sw=2 et:
